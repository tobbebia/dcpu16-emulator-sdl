; FROM http://0x10cforum.com/forum/m/4932880/viewthread/3876116-basic-teletype
; Added interrupt to set video ram address to 8000


JSR boot_ttys

SET A, 0
SET B, 0x8000
HWI [tty_device_id]

SET A, tty_1
SET B, 0x8000
JSR initialize_tty
SET B, 9
SET C, 2
JSR set_color
JSR blink_off

SET A, tty_2
SET B, 0x8180
JSR initialize_tty
SET B, 14
SET C, 12
JSR set_color
JSR blink_on

show_font:
SET A, tty_1
JSR switch_to_tty
SET C, 0
show_font_loop:
JSR putc
ADD C, 1
SET X, 512
show_font_wait_loop:
SUB X, 1
IFN X, 0
  SET PC, show_font_wait_loop
IFL C, 128
  SET PC, show_font_loop

show_message:
SET A, tty_2
JSR switch_to_tty
SET B, message
show_message_loop:
SET C, [B]
IFE C, 0
  SET PC, show_message_end
JSR putc
ADD B, 1
SET X, 512
show_message_wait_loop:
SUB X, 1
IFN X, 0
  SET PC, show_message_wait_loop
SET PC, show_message_loop
show_message_end:
JSR newline
SET X, 0xffff
show_message_hold_loop:
SUB X, 1
IFN X, 0
  SET PC, show_message_hold_loop

SET PC, show_font

stopexe:
SET PC, stopexe

; Metadata for teletypes 1 and 2 (address, cursor address, color/blink)
tty_1:
DAT 0, 0, 0
tty_2:
DAT 0, 0, 0
message:
DAT "Hello there!", 0

; A: teletype metadata address (three words long)
; B: address of start of video RAM
initialize_tty:
SET [A], B
JSR clear_tty
SET [A+2], 0x7000 ;standard white-on-black text
SET PC, POP

; A: teletype metadata address
switch_to_tty:
SET PUSH, B
SET PUSH, A
SET B, [A]
SET A, 0
HWI [tty_device_id]
SET A, POP
SET B, POP
SET PC, POP

; A: teletype metadata address
clear_tty:
SET PUSH, J
SET PUSH, I
SET PUSH, B
SET B, [A+2]
BOR B, 32
SET I, [A]
SET [A+1], I
ADD I, 383
SET J, 384
__clear_tty__loop:
STD [I], B
IFN J, 0
  SET PC, __clear_tty__loop
SET B, POP
SET I, POP
SET J, POP
SET PC, POP

; A: teletype metadata address
; B: background color
; C: color
set_color:
SET PUSH, C
SET PUSH, B
SHL C, 12
AND B, 0x000f
SHL B, 8
BOR B, C
SET C, [A+2]
AND C, 0x00ff
BOR C, B
SET [A+2], C
SET B, POP
SET C, POP
SET PC, POP

; A: teletype metadata address
blink_on:
BOR [A+2], 0x0080
SET PC, POP

; A: teletype metadata address
blink_off:
AND [A+2], 0xff7f
SET PC, POP

; A: teletype metadata address
; C: character
putc:
SET PUSH, C
SET PUSH, B
SET B, [A+1]
SUB B, [A]
IFG B, 383
  JSR scroll_up
SET B, [A+1]
AND C, 0x007f
BOR C, [A+2]
SET [B], C
ADD [A+1], 1
SET B, POP
SET C, POP
SET PC, POP

; A: teletype metadata address
newline:
SET PUSH, C
SET PUSH, B
SET B, [A+1]
SET C, [A]
SUB B, C
AND B, 0xffe0
ADD B, 0x0020
IFN B, 384
  SET PC, __newline__no_scroll_up
ADD B, C
SET [A+1], B
JSR scroll_up
SET PC, __newline__exit
__newline__no_scroll_up:
ADD B, C
SET [A+1], B
__newline__exit:
SET B, POP
SET C, POP
SET PC, POP

; A: teletype metadata address
scroll_up:
SET PUSH, J
SET PUSH, I
SET PUSH, C
SET PUSH, B
SET B, [A]
ADD B, 384
SET I, [A]
SET J, I
ADD J, 32
__scroll_up__copy_loop:
STI [I], [J]
IFN J, B
  SET PC, __scroll_up__copy_loop
SET C, [A+2]
BOR C, 32
__scroll_up__blank_loop:
STI [I], C
IFN I, B
  SET PC, __scroll_up__blank_loop
SET B, [A+1]
SUB B, 32
IFL B, [A]
  SET B, [A]
SET [A+1], B
SET B, POP
SET C, POP
SET I, POP
SET J, POP
SET PC, POP

boot_ttys:
SET PUSH, I
SET PUSH, Y
SET PUSH, X
SET PUSH, C
SET PUSH, B
SET PUSH, A
HWN I

__boot_ttys__loop:
SUB I, 1
HWQ I
IFE A, 0xf615
  IFE B, 0x7349    
    SET [tty_device_id], I

IFN I, 0
  SET PC, __boot_ttys__loop
SET A, POP
SET B, POP
SET C, POP
SET X, POP
SET Y, POP
SET I, POP
SET PC, POP

tty_device_id:
dat 0x1234
